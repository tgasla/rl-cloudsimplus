/*
 * This build file was generated by the Gradle 'init' task.
 *
 * This generated file contains a commented-out sample Java project to get you started.
 * For more details take a look at the Java Quickstart chapter in the Gradle
 * user guide available at https://docs.gradle.org/current/userguide/userguide.html
 */

// Apply the java plugin to add support for Java
plugins {
  id 'java'
  id 'application'
  id 'com.bmuschko.docker-java-application' version '9.4.0'
}

//https://stackoverflow.com/questions/29593500/how-can-i-set-the-compileoptions-for-my-gradle-java-plugin
tasks.withType(JavaCompile) {
    configure(options) {
        options.compilerArgs << '-Xlint:deprecation' << '-Xlint:unchecked' << '-Xdiags:verbose'
    }
}

task configureLogging {
    doLast {
        def logLevel = System.getProperty('log.level', 'INFO')
        def logbackFile = file('src/main/resources/logback.xml')
        if (!logbackFile.exists()) {
            logger.warn "Logback configuration file not found."
            return
        }
        def logbackConfig = logbackFile.text
        logbackConfig = logbackConfig.replaceAll('<root level="\\w+">', "<root level=\"$logLevel\">")
        logbackFile.write(logbackConfig)
        logger.lifecycle "Logging level set to $logLevel"
    }
}

build.dependsOn configureLogging

test {
    useJUnitPlatform()
    // enable/disable system out messages on junit tests
    // enable testLogging if log.level == 'DEBUG'
    // testLogging.showStandardStreams = System.getProperty('log.level', 'INFO') == 'DEBUG'
    testLogging.showStandardStreams = false
}

// In this section you declare where to find the dependencies of your project

repositories {
    mavenCentral()
}

// In this section you declare the dependencies for your production and test code
dependencies {
    // The production code uses the SLF4J logging API at compile time
    implementation 'org.cloudsimplus:cloudsimplus:8.5.2'
    implementation 'org.slf4j:slf4j-api:2.0.16'
    implementation 'net.sf.py4j:py4j:0.10.9.7'
    implementation 'ch.qos.logback:logback-classic:1.5.7'
    implementation 'org.apache.commons:commons-collections4:4.5.0-M2'
    implementation 'com.google.code.gson:gson:2.11.0'
    
    testImplementation 'commons-io:commons-io:2.16.1'
    testImplementation 'org.hamcrest:hamcrest:3.0'
    testImplementation 'org.junit.jupiter:junit-jupiter-engine:5.11.0'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher:1.11.0'
}

def dockerImageTag = 'gateway:2.0.0'
def os = System.getProperty('os.name').toLowerCase()
def totalMemorySizeInGigabytes

if (os.contains('linux')) {
    def totalMemorySizeInBytes = "cat /proc/meminfo | grep MemTotal".execute().text.split(":")[1].trim().split()[0].toLong() * 1024
    totalMemorySizeInGigabytes = Math.round(totalMemorySizeInBytes / (1024 * 1024 * 1024))
} else if (os.contains('mac')) {
    def totalMemorySizeInBytes = "sysctl -n hw.memsize".execute().text.trim().toLong()
    totalMemorySizeInGigabytes = Math.round(totalMemorySizeInBytes / (1024 * 1024 * 1024))
} else {
    throw new IllegalStateException("Unsupported operating system: $os")
}

application {
    mainClass.set('daislab.cspg.Main')
}

docker {
    url = 'unix:///var/run/docker.sock'
    javaApplication {
        baseImage = 'amazoncorretto:21-alpine-jdk'
        images = [dockerImageTag]
        jvmArgs = ["-Xms1G", "-Xmx${totalMemorySizeInGigabytes}G"]
    }
}

// You can add environment variables either here or in the docker-compose.yml
dockerCreateDockerfile {
    environmentVariable 'JAVA_OPTS',
        '-XX:+UnlockExperimentalVMOptions ' +
        '-XX:+UseCGroupMemoryLimitForHeap ' +
        '-XX:+HeapDumpOnOutOfMemoryError ' +
        '-XX:HeapDumpPath=/external'
}

java {
    sourceCompatibility = JavaVersion.VERSION_21
    targetCompatibility = JavaVersion.VERSION_21
}
